group 'com.github.cloveros'
version '1.1.0'

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'


sourceCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

processResources {
    from('src/main/java') {
        include '**/*.*'
    }
}
jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}


javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'EasyApi'
                packaging 'jar'
                description 'A RESTFUL Framework for JavaWeb'
                url 'https://github.com/CloverOS/EasyApiFramework'

                scm {
                    connection 'scm:git:git://github.com/CloverOS/EasyApiFramework.git'
                    developerConnection 'scm:git:ssh://github.com:CloverOS/EasyApiFramework.git'
                    url 'https://github.com/CloverOS/EasyApiFramework/tree/master'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'khthink'
                        name 'kh'
                        email '527756694@qq.com'
                        organization 'Sonatype'
                        organizationUrl 'http://www.sonatype.com'
                    }
                }
            }
        }
    }
}

signing {
    sign configurations.archives
}

dependencies {
    compile group: 'commons-fileupload', name: 'commons-fileupload', version: '1.3.3'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'org.apache.tomcat', name: 'tomcat-servlet-api', version: '8.5.30'
    compile group: 'redis.clients', name: 'jedis', version: '2.9.0'
    compile group: 'com.zaxxer', name: 'HikariCP', version: '3.1.0'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'
    compile group: 'mysql', name: 'mysql-connector-java', version: '8.0.11'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'com.alibaba:fastjson:1.2.46'
    compile 'org.mybatis:mybatis:3.4.6'
    compile "io.reactivex.rxjava2:rxjava:2.1.13"
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.20'
}
